#!/usr/bin/env python

Import("env")

env.editor_sources = []

import os
import os.path
import glob
import editor_builders


def _make_doc_data_class_path(to_path):
    # NOTE: It is safe to generate this file here, since this is still executed serially
    g = open(os.path.join(to_path, "doc_data_class_path.gen.h"), "w", encoding="utf-8")
    g.write("static const int _doc_data_class_path_count = " + str(len(env.doc_class_path)) + ";\n")
    g.write("struct _DocDataClassPath { const char* name; const char* path; };\n")

    g.write("static const _DocDataClassPath _doc_data_class_paths[" + str(len(env.doc_class_path) + 1) + "] = {\n")
    for c in sorted(env.doc_class_path):
        g.write('\t{"' + c + '", "' + env.doc_class_path[c] + '"},\n')
    g.write("\t{nullptr, nullptr}\n")
    g.write("};\n")

    g.close()


if env["tools"]:
    # Register exporters
    reg_exporters_inc = '#include "register_exporters.h"\n'
    reg_exporters = "void register_exporters() {\n"
    for e in env.platform_exporters:
        env.add_source_files(env.editor_sources, "#platform/" + e + "/export/export.cpp")
        reg_exporters += "\tregister_" + e + "_exporter();\n"
        reg_exporters_inc += '#include "platform/' + e + '/export/export.h"\n'
    reg_exporters += "}\n"

    # NOTE: It is safe to generate this file here, since this is still executed serially
    with open("register_exporters.gen.cpp", "w", encoding="utf-8") as f:
        f.write(reg_exporters_inc)
        f.write(reg_exporters)

    # Core API documentation.
    docs = []
    docs += Glob("#doc/classes/*.xml")

    # Module API documentation.
    module_dirs = []
    for d in env.doc_class_path.values():
        if d not in module_dirs:
            module_dirs.append(d)

    for d in module_dirs:
        if not os.path.isabs(d):
            docs += Glob("#" + d + "/*.xml")  # Built-in.
        else:
            docs += Glob(d + "/*.xml")  # Custom.

    _make_doc_data_class_path(os.path.join(env.Dir("#").abspath, "editor"))

    docs = sorted(docs)
    env.Depends("#editor/doc_data_compressed.gen.h", docs)
    env.CommandNoCache(
        "#editor/doc_data_compressed.gen.h",
        docs,
        env.Run(editor_builders.make_doc_header, "Generating documentation header."),
    )

    path = env.Dir(".").abspath

    # Editor translations
    tlist = glob.glob(path + "/translations/*.po")
    env.Depends("#editor/editor_translations.gen.h", tlist)
    env.CommandNoCache(
        "#editor/editor_translations.gen.h",
        tlist,
        env.Run(editor_builders.make_editor_translations_header, "Generating editor translations header."),
    )

    # Documentation translations
    tlist = glob.glob(env.Dir("#doc").abspath + "/translations/*.po")
    env.Depends("#editor/doc_translations.gen.h", tlist)
    env.CommandNoCache(
        "#editor/doc_translations.gen.h",
        tlist,
        env.Run(editor_builders.make_doc_translations_header, "Generating translations header."),
    )

    # Fonts
    flist = glob.glob(path + "/../thirdparty/fonts/*.ttf")
    flist.extend(glob.glob(path + "/../thirdparty/fonts/*.otf"))
    flist.sort()
    env.Depends("#editor/builtin_fonts.gen.h", flist)
    env.CommandNoCache(
        "#editor/builtin_fonts.gen.h",
        flist,
        env.Run(editor_builders.make_fonts_header, "Generating builtin fonts header."),
    )

    env.add_source_files(env.editor_sources, "*.cpp")

    SConscript("debugger/SCsub")
    SConscript("fileserver/SCsub")
    SConscript("icons/SCsub")
    SConscript("import/SCsub")
    SConscript("plugins/SCsub")

    lib = env.add_library("editor", env.editor_sources)
    env.Prepend(LIBS=[lib])
